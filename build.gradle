/*
 * Copyright (c) 2016.
 * This file is part of SourceBot.
 *
 * SourceBot is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * SourceBot is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with SourceBot.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'application'

group 'net.walterbarnes'
mainClassName = group + ".sourcebot.SourceBot"
version '0.2'

def workingBranch = """git rev-parse --abbrev-ref HEAD""".execute().text.trim()
if (!workingBranch.equals("master")) {
    version = workingBranch + "-" + version
}

// If TeamCity is running this build, lets set the version info
def buildNum = '0'
if (hasProperty("teamcity")) {
    //noinspection GrUnresolvedAccess
    buildNum = teamcity["build.number"]
    println "##teamcity[buildNumber '" + version + "." + buildNum + "']"
}
version = version + "." + buildNum

//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.8
//noinspection GroovyUnusedAssignment
targetCompatibility = 1.8

repositories {
    //mavenLocal()
    maven {
        url "http://maven.walterbarnes.net:8081/nexus/content/repositories/snapshots"
    }
    maven {
        url "http://maven.walterbarnes.net:8081/nexus/content/groups/public"
    }
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.11'
    compile 'com.github.onyxfoxdevelopment:OFlib:0.2.17'
    compile 'com.tumblr:jumblr:+'
    runtime 'org.postgresql:postgresql:9.4.1208'
    compile 'commons-cli:commons-cli:1.3'
    compile 'org.eclipse.jetty:jetty-server:9.3.7.v20160115'
    compile 'org.slf4j:slf4j-simple:1.7.21'
    compile 'com.j2html:j2html:0.7'
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile 'com.google.code.gson:gson:2.2.2'

}

//noinspection GroovyAssignabilityCheck
jar {
    manifest {
        attributes('Main-Class': mainClassName
        )
    }
    baseName = rootProject.name
    version = version
}

//noinspection GroovyAssignabilityCheck
processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'sbversion.properties'
        expand 'version': project.version
    }
    //noinspection GrUnresolvedAccess
    from(sourceSets.main.resources.srcDirs) {
        exclude 'sbversion.properties'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}
